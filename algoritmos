# studoodles
Registrando meu estudos iniciais em algoritmos

Algoritmos
São conjuntos de passos finitos e organizados que, quando executados, resolvem um determinado problema.
São passos a serem seguidos por um módulo processador e seus respectivos usuários que, quando executados na ordem correta, conseguem realizar determinada tarefa.

Módulo Processador: Tudo aquilo que pode efetuar processamento. Tudo aquilo que pode ser programável.
Usuários: Quem utiliza > client.
Realizar: Resolver um problema/necessidade (tarefa).

Algoritmos computacionais são rotinas realizadas por algum processador. 

Pseudocódigo ou "Portugol" - Lógica de Programação - é uma forma de representar a lógica do programa antes de passar para uma linguagem de programação.

_____________________________________________________________________________________________________________

  Variáveis
	- Tipo de dado armazenado no computador;
	- O valor contido na memória do computador varia com o tempo, ou seja, não é um valor fixo.
	- Ex: RAM- Armazena dados temporários.
	- Ao declarar uma variável em um programa, estamos definindo e reservando um espaço na memória para armazenar o valor que aquela variável conterá em determinado tempo de execução do programa.
	
  Contantes
	- Armazena valores únicos, que não mudará com o tempo de execução do programa.

_____________________________________________________________________________________________________________

Tipos
	- Tipo númerico -Int
Armazenamento de números, podendo ser utilizados para cálculos.

Podem ser classificados como inteiros e reais(Double-possuem casas decimais).

	- Tipo Caractere -Char

Sequencia de caracteres que contêm apenas letras. Não é possivel ter números ou simbolos especiais.

	- Tipo Alfanumerico -String

Letras, números, digitos ou simbolos especiais.

São delimitados com aspas.

	- Tipo Lógico -Bool

Representa dois únicos valores lógicos que são possíveis: o verdadeiro ou o falso / sim ou não / 1 ou 0 / true or false

_____________________________________________________________________________________________________________

Declaração
	-  o nome que será dado a variável ou constante;
	-  associar esse nome a um tipo de dado (numérico/caractere/string/lógico).

Exemplo:

Variável Idade Tipo Inteiro

Variável Velocidade Tipo Real

_____________________________________________________________________________________________________________

Operadores
igual: ==

diferente: !=

*exclamação antes do símbolo NEGA o que vem em seguida*

menor: <

maior: >

menor igual: <=

maior igual: >=

_____________________________________________________________________________________________________________

- PARA (for): quando sabemos a quantidade de vezes que teremos que mostrar aquela informação (quantidade de repetições). 

ex:

- for
(var i = 0; i <= 10; i++)
{
  Console.WriteLine(i);
}

- Repita / Enquanto (while): 

var cont = 0;

while (cont < 10)
{
  Console.WriteLine("Beatriz");
  Cont++;
}

- Repita até (do while): 

var cont = 0;

do
{
   Console.WriteLine("Beatriz");
   cont++;
}
while(cont < 5)

- Para cada (foreach) - mais voltado para usar em vetores e matrizes.

{
     string[] nomes = new string { "Marcos", "Joaquim", "Amanda", "Marcela" }
    
     foreach(string nome in nomes)
     {
           Console.WriteLine(nome);
     }
 }
 
_____________________________________________________________________________________________________________
 
Arrays
- Vetores (Array Simples): Estrutura que permite armazenar informações de forma ordenada/sequenciada. Permite criar uma lista de variáveis do mesmo tipo. Uma dimensão.
ex:
{
string[] nomes= { "Maria", "Amanda", "Roberta", "Fernando" };

        Console.WriteLine(nomes[3]);
}
        A "resposta" seria: Fernando (a contagem começa do 0, 1, 2, 3).

ex usando laço for:
{
string[] nomes= { "Maria", "Amanda", "Roberta", "Fernando" };

for(int i = 0; i < nomes.Length; i++)
 {
        Console.WriteLine(nomes[i]);
 }
}

Resposta: todos os nomes / "Length" mostra o comprimento, tudo o que consta dentro de "nomes".

- Matrizes (Array Composto): Estrutura encadeada de vetores. Permite criar uma lista de vetores do mesmo tipo. Tridimensional. Uma matriz, fisicamente, se parece com uma tabela, os elementos são acessados através de coordenadas (X;Y).
ex:
string[,] nome: new string[3, 3];

for (int linha = 0; linha < 3; linha++)
{
      for (int coluna = 0; coluna < 3; coluna++)
      {
                 nome[linha, coluna] = Console.ReadLine();
      }
}
      
      Console.WriteLine();

for (int linha = 0; linha < 3; linha++)
{
      for (int coluna = 0; coluna < 3; coluna++)
      {
               Console.WriteLine(nome[linha, coluna] + " ");
      }
      Console.WriteLine();
}

_____________________________________________________________________________________________________________

Exercicios

Faça um Programa que peça um número e então mostre a mensagem: O número informado foi [número]. 

var numero = prompt("Digite um número")
    alert("Você digitou o número " + numero)

Faça um Programa que converta metros para centímetros. (Dica: 1 metro = 100 centímetros)

var numerocen = prompt("Digite os centimetros")
var resultado = numerocen / 100
    alert("Resultado:" + resultado + " Metros")

Faça um Programa que converta centímetros para polegadas. (Dica: 1 centímetro = 0,393700787 polegadas)

var centimetros = prompt("Digite os centimetros:")
var resultado = centimetros * 0.393700787
    alert("O resultado é = " + resultado + " polegadas")

Faça um Programa que peça o raio de um círculo, calcule e mostre sua área. Fórmula: Area = pi * raio²

var raioCirculo = prompt("Digite o raio do circulo")
var resultado = 3.14 * (raioCirculo * raioCirculo)
    alert("Resultado da área = " + resultado)

Faça um Programa que peça a temperatura em graus Farenheit, transforme e mostre a temperatura em graus Celsius. Fórmula: Celsius = (5 * (Farenheit -32) / 9)

var temperaturaF = prompt("Digite a temperatura em Farenheit")
var resultado = (5 * (temperaturaF -32) / 9)
    alert("Celsius" + resultado)

Faça um Programa que peça as 4 notas bimestrais e mostre a média.

var Nota1 = parseFloat(prompt("Digite a nota 1"))
var Nota2 = parseFloat(prompt("Digite a nota 2"))
var Nota3 = parseFloat(prompt("Digite a nota 3"))
var Nota4 = parseFloat(prompt("Digite a nota 4"))
resultado = (Nota1 + Nota2 + Nota3 + Nota4) / 4
    alert("Média = " + resultado)

Faça um Programa que peça a temperatura em graus Celsius, transforme e mostre em graus Farenheit. Fórmula : Farenheit = Celsius × 1,8 + 32

var temperaturaC = prompt("Digite os Graus Celsius")
var resultado = (temperaturaC * 1.8) + 32
    alert("F = " + resultado)

Faça um Programa que peça o valor da gasolina e do álcool de um posto e diga qual é o combustível mais vantajoso abastecer, sabendo que somente é vantagem abastecer álcool se o preço do mesmo é menor ou igual a 70% do valor da gasolina.

var gasolina = parseFloat(prompt("Preço da gasolina"))
var alcool = parseFloat(prompt("Preço do álcool"))
var resultado = (70 / 100) * gasolina

    if(alcool <= resultado) {
        alert("É mais vantajoso colocar álcool e não gasolina")

    }else {
        alert("É mais vantajoso colocar gasolina e não álcool")

    }
_____________________________________________________________________________________________________________

Exercícios - Curso em vídeo

4) Desenvolva um algoritmo que leia dois números inteiros e mostre o somatório entre eles. 

Algoritmo "soma"
Var
       N1, N2, S: inteiro
Inicio
      Escreva("Digite um número: ")
      Leia(N1)
      Escreva("Digite outro número: ")
      Leia(N2)
      S <- N1 + N2
      Escreva("A soma entre ", N1," e ", N2," é igual a ", S)
Fimalgoritmo 

5) Faça um programa que leia as duas notas de um aluno em uma matéria e mostre na tela a sua média na disciplina.

Algoritmo "média"

Var
    N1, N2, M: real
Inicio
    Escreva("Digite a nota da P1: ")
    Leia(N1)
    Escreva("Digite a nota da P2: ")
    Leia(N2)
    M <- (N1 + N2)/2
    Escreva("A média do aluno é: ", M)

Fimalgoritmo

6) Faça um programa que leia um número inteiro e mostre o seu antecessor e seu sucessor.

Algoritmo "ante/suce"

Var
     N1, Ant, Suc: inteiro
Inicio
     Escreva("Digite um número: ")
     Leia(N1)
     Ant <- N1 - 1
     Suc <- N1 + 1
     Escreval("O antecessor de ", N1, " é: ", Ant)
     Escreva("O sucessor de ", N1, " é: ", Suc)

Fimalgoritmo

7) Crie um algoritmo que leia um número real e mostre na tela o seu dobro e a sua terça parte.

Algoritmo "exerc7"

Var
    N1, D2, TP: real
Inicio
    Escreva("Digite um número: ")
    Leia(N1)
    D2 <- N1*2
    Escreval("O dobro de ", N1, " é", D2)
    TP <- N1/3
    Escreva("A terça parte de ", N1, " é", TP)

Fimalgoritmo

8) Desenvolva um programa que leia uma distância em metros e mostre os valores relativos em outras medidas. 

Ex:  Digite uma distância em metros: 185.72 
A distância de 85.7m corresponde a: 0.18572Km 1.8572Hm 18.572Dam 1857.2dm 18572.0cm 185720.0mm
Algoritmo "exerc8"

Var
   M, Km, In, Cm, Mm: real
Inicio
   Escreva("Digite uma distância em metros: ")
   Leia(M)
   Km <- M/1000
   In <- M*39.370
   Cm <- M/0.010000
   Mm <- M/0.0010000
   Escreva("A distância de ", M, " metros", " corresponde a:", Km, "Km,", In, " polegadas,", Cm, "cm,", Mm, "mm.")

Fimalgoritmo

9) Faça um algoritmo que leia quanto dinheiro uma pessoa tem na carteira (em R$) e mostre quantos dólares ela pode comprar. Considere US$1,00 = R$5.56.
Algoritmo "exerc9"

Var
   dinCart, dinDolar: real
Inicio
   Escreva("Digite quantos reais você deseja converter para dolars: R$ ")
   Leia(dinCart)
   dinDolar <- dinCart/5.56
   Escreva("R$", dinCart:5:2, " é igual a US$", dinDolar:5:2)

Fimalgoritmo

10) Faça um algoritmo que leia a largura e altura de uma parede, calcule e mostre a área a ser pintada e a quantidade de tinta necessária para o serviço, sabendo que cada litro de tinta pinta uma área de 2metros quadrados.
Algoritmo "exerc10"

Var
   altP, larP, areaP, tinta: real
Inicio
   Escreva("Qual a altura da parede: ")
   Leia(altP)
   Escreva("Qual a largura da parede: ")
   Leia(larP)
   areaP <- altP*larP
   tinta <- areaP/2
   Escreva("Para pintar as paredes, vocês usará ", tinta, " litros de tinta")

Fimalgoritmo

11) Desenvolva uma lógica que leia os valores de A, B e C de uma equação do segundo grau e mostre o valor de Delta.

resultado = b * 2 - 4 * a * c
Algoritmo "exerc8"

Var
   A, B, C, Delta: real
Inicio
   Escreva("Qual o valor de A: ")
   Leia(A)
   Escreva("Qual o valor de B: ")
   Leia(B)
   Escreva("Qual o valor de C: ")
   Leia(C)
   Delta <- B*2-4*A*C
   Escreva("O valor de Delta é: ", Delta)

Fimalgoritmo

12) Crie um programa que leia o preço de um produto, calcule e mostre o seu PREÇO PROMOCIONAL, com 5% de desconto.
13) Faça um algoritmo que leia o salário de um funcionário, calcule e mostre o seu novo salário, com 15% de aumento.
14) A locadora de carros precisa da sua ajuda para cobrar seus serviços. Escreva um programa que pergunte a quantidade de Km percorridos por um carro alugado e a quantidade de dias pelos quais ele foi alugado. Calcule o preço total a pagar, sabendo que o carro custa R$90 por dia e R$0,20 por Km rodado.
15) Crie um programa que leia o número de dias trabalhados em um mês e mostre o salário de um funcionário, sabendo que ele trabalha 8 horas por dia e ganha R$25 por hora trabalhada

_____________________________________________________________________________________________________________

Aula 7 (Curso em vídeo) - Estruturas Condicionais 

Condicional Simples: Quando existe um bloco sendo executado caso uma expressão seja verdadeira.
Se (x) entao
          Escreval("y")
FimSe

Condicional Composta:Quando incorpora o "senao" na sintaxe.
Se (x) entao
          Escreval("y")
senao
	Escreval("z")
FimSe

Exercícios: 

algoritmo "QuantosAnos"
var
      Nasc, Atual, ID: inteiro
inicio
      Escreva("Em que ano estamos? ")
      Leia(Atual)
      Escreva("Em que ano eu nasci? ")
      Leia(Nasc)
      ID <- Atual - Nasc
      Escreva("Eu tenho", ID, " anos, ")
      Se (ID >= 21) entao
         Escreval("já posso beber nos USA!!")
      senao
         Escreval("não posso beber nos USA :(")
      FimSe
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "ParOuImpar"
var
N1: Inteiro
R: Real
inicio
Escreval("------------------")
Escreval("---PAR OU IMPAR---")
Escreval("------------------")
Escreval("                  ")
Escreva("Digite um número: ")
Leia(N1)
Se (N1 % 2 = 0) entao
   Escreval("Par!")
senao
   Escreval("Impar!")
FimSe
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "IMC"
var
M, A, IMC: Real

inicio
      Escreva("Digite sua massa (Kg): ")
      Leia(M)
      Escreva("Digite sua altura (m): ")
      Leia(A)
      IMC <- M / A ^ 2
      Escreval("Seu IMC é: ", IMC:5:2)
      Se (IMC >= 18.5) e (IMC < 25) entao
           Escreval("Parabéns, você está no seu peso ideal!")
      senao
           Escreval("Você não está no seu peso ideal.")
      FimSe
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "AptoDirigir"
var
anoA, anoN, ID: inteiro
inicio
Escreval("-----------------------")
Escreval("-----ESCOLA RODAPÉ-----")
Escreval("-----------------------")
Escreval("                   ")
Escreval("-----------------------")
Escreva("Ano atual (yyyy): ")
Leia(anoA)
Escreva("Ano de nascimento (yyyy): ")
Leia(anoN)
ID <- anoA - anoN
Escreval("                   ")
Escreval("--------STATUS--------")
EscrevaL("IDADE: ", ID, " ANOS.")
Se (ID > 18) entao
   EscrevaL("APTO A TIRAR A CARTA.")
senao
   EscrevaL("INAPTO A TIRAR A CARTA.")
FimSe
Escreval("-----------------------")
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "media"
var
nomeA: caractere
P1, P2, M: real
inicio
Escreval("-------------------------")
Escreval("-----ESCOLA RORAIMAR-----")
Escreval("-------------------------")
Escreval("                   ")
Escreval("-------------------------")
Escreva("Digite o nome do aluno: ")
Leia(nomeA)
Escreva("Digite a nota da P1 de ", nomeA, ": ")
Leia(P1)
Escreva("Digite a nota da P2 de ", nomeA, ": ")
Leia(P2)
M <- (P1+P2)/2
Escreval("                   ")
Escreval("-------------------------")
Escreval("MÉDIA: ", M)
Se (M >=7) entao
   Escreval(nomeA, ", ESTA APROVADX!")
senao
   Escreval(nomeA, ", ESTA REPROVADX!")
FimSe
Escreval("-------------------------")
fimalgoritmo
_____________________________________________________________________________________________________________

Aula 8 (Curso em vídeo) - Estruturas Condicionais 2

Condicional aninhada: Quando há uma estrutura dentro da outra.
Se (x) e (y) entao
       EscrevaL("z")
senao
          Se (a) entao
                  EscrevaL("b")
          senao
		(c)
          FimSe
FimSe

Escolha / Caso: Quando se utiliza "Caso" - Ideal para usar com variáveis inteiras (não serve para testar faixa de valores "x >= y" ).
Escolha D
        Caso 1
            Valor <- 10
        Caso 2
            Valor <- 25
        Caso 3
            Valor <- 50
        Caso 4
            Escreva("Qual o valor da doação? R$ ")
            Leia(Valor)
        Caso 5
            Valor <- 0
      FimEscolha

Exercícios: 

algoritmo "médiaescolar"
var
    N1, N2, N3, M: real
inicio
       Escreva("Digite sua nota da P1: ")
       Leia(N1)
       Escreva("Digite sua nota da P2: ")
       Leia(N2)
       Escreva("Digite sua nota do trabalho em grupo: ")
       Leia(N3)
       M <- (N1+N2+N3)/3
       Escreval("Sua média final é: ", M:3:2)
       Se (M>=7) entao
          Escreval("Você passou de ano!! Boas férias!")
       senao
            Se (M >= 5) e (M < 7) entao
               Escreval("Você está de recuperação.")
            senao
               Escreva("Você está reprovado.")
            fimse
       fimse
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "médiaescolar"
var
N1, N2, M: Real
inicio
      Escreva("Digite a nota da P1: ")
      Leia(N1)
      Escreva("Digite a nota da P2: ")
      Leia(N2)
      M <- (N1 + N2)/2
       Se (M >= 7) entao
          Escreval("Aprovadx!")
       senao
            Se (M >=5)e (M < 7) entao
               Escreval  ("Recuperação!")
            senao
               Escreval("Reprovadx!")
            FimSe
       FimSe
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "IMC2"
var
M, A, IMC: Real
inicio
      Escreva("Massa (Kg): ")
      Leia(M)
      Escreva("Altura (m): ")
      Leia(A)
      IMC <- M / (A^2)
      Escreval("IMC: ", IMC:5:2)
      Se (IMC < 17) entao
         Escreval("Muito abaixo do peso!")
      senao
           Se (IMC >= 17) e (IMC < 18.5) entao
              Escreval("Abaixo do peso!")
           senao
                Se (IMC >= 18.5) e (IMC < 25) entao
                   Escreval("Dentro do seu peso ideal!")
                senao
                     Se (IMC >= 25) e (IMC < 30) entao
                         Escreval("Sobrepeso!")
                     senao
                          Se (IMC >= 30) e (IMC < 35) entao
                             Escreval("Obesidade!")
                          senao
                               Se (IMC >= 35) e (IMC < 40) entao
                                  Escreval("Obesidade severa!")
                               senao
                                    Escreval("Obesidade mórbida!")
                               FimSe
                          FimSe
                     FimSe
                FimSe
           FimSe
      FimSe
fimalgoritmo
---
algoritmo "casoDoacao"
var
D: Inteiro
Valor: Real
inicio
      Escreval ("[1] para doar R$10")
      Escreval ("[2] para doar R$25")
      Escreval ("[3] para doar R$50")
      Escreval ("[4] para doar outros valores")
      Escreval ("[5] para cancelar")
      Leia(D)
      Escolha D
        Caso 1
            Valor <- 10
        Caso 2
            Valor <- 25
        Caso 3
            Valor <- 50
        Caso 4
            Escreva("Qual o valor da doação? R$ ")
            Leia(Valor)
        Caso 5
            Valor <- 0
      FimEscolha
      Escreval("Sua doação foi de R$ ", Valor:5:2)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "escolha"
var
     Nome: Caractere
     ASal, NSal: Real
     Dep: Inteiro
inicio
     Escreva("Digite o nome do funcionário: ")
     Leia(Nome)
     Escreva("Digite o salário atual do funcionário: R$ ")
     Leia(ASal)
     Escreva("Quantos dependentes este funcionário tem?")
     Leia(Dep)
     Escolha Dep
             Caso 0
                  NSal <- ASal + (ASal*5/100)
             Caso 1, 2, 3
                  NSal <- ASal + (ASal*10/100)
             Caso 4, 5, 6
                  NSal <- ASal + (ASal*15/100)
             OutroCaso
                  NSal <- ASal + (ASal*18/100)
     FimEscolha
     Escreva("O novo salário de ", Nome," é: ", NSal)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "Aproveitamento"
var
    N1, N2, M: Real
inicio
  EscrevaL("------------------------------------")
  EscrevaL("     E.E. MARIA IRACEMA MUNHOZ      ")
  EscrevaL("------------------------------------")
      
      Escreva("Nota da P1: ")
      Leia(N1)
      Escreva("Nota da P2: ")
      Leia(N2)
      M <- (N1+N2)/2
      EscrevaL("------------------------------------")
      EscrevaL("MÉDIA:", M)
      Se (M>=9) entao
         EscrevaL("APROVEITAMENTO: A")
      senao
           Se (M>=8) e (M<8.9) entao
              EscrevaL("APROVEITAMENTO: B")
           senao
                Se (M>=7) e (M<7.9) entao
                   EscrevaL("APROVEITAMENTO: C")
                senao
                     Se (M>=6) e (M<6.9) entao
                        EscrevaL("APROVEITAMENTO: D")
                     senao
                          Se (M>=5) e (M<5.9) entao
                             EscrevaL("APROVEITAMENTO: E")
                          senao
                               Se (M<5) entao
                                  EscrevaL("APROVEITAMENTO: F")
                               senao
                               FimSe
                          FimSe
                     FimSe
                FimSe
           FimSe
      FimSe
      EscrevaL("------------------------------------")
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "Gols"
var
corn, sant, D: inteiro
inicio
Escreval("CORINTIA x SANTU")
Escreval("----------------")
Escreva("Quantos gols do CORINTIA? ")
Leia(corn)
Escreva("Quantos gols do SANTU? ")
Leia(sant)
Se (corn > sant) entao
   D <- corn - sant
senao
     Se (sant > corn) entao
        D <- sant - corn
     FimSe
FimSe
Escreval("DIFERENÇA: ", D)
Se (D > 0 ) e (D <= 4) entao
   Escreval("Partida normal!")
senao
     Se (D = 0) entao
        Escreval("Empate!")
     senao
          Se (D > 4) entao
             Escreva ("Goleada!")
          FimSe
     FimSe
FimSe
fimalgoritmo
_____________________________________________________________________________________________________________

Aula 9 (Curso em vídeo) - Estruturas de Repetição 1

algoritmo "contador"
var
    cont, N, S: inteiro
inicio
    cont <- 0
    Escreva("Quer contar até quanto? ")
    Leia(N)
    Escreva("Qual o valor do salto? ")
    Leia(S)
    Enquanto (cont <= N) faca
        EscrevaL(cont)
        cont <- cont + S
    FimEnquanto
fimalgoritmo

_____________________________________________________________________________________________________________

algoritmo "contador2"
var
    cont, N, S: inteiro
inicio
    cont <- 1
    S <- 0
    Enquanto (cont <= 5) faca
        Escreva("Digite o ", cont, "o. valor: ")
        Leia(N)
        S <- S + N
        cont <- cont + 1
    FimEnquanto
    Escreval("A soma de todos os valores é: ", S)
fimalgoritmo

_____________________________________________________________________________________________________________

algoritmo "contador"
var
    cont, N, S, maior, menor: inteiro
inicio
    cont <- 1
    S <- 0
    Enquanto (cont <= 5) faca
        Escreva("Digite o ", cont, "o. valor: ")
        Leia(N)
        Se (N > maior) entao
            maior <- N
         senao
           Se (0 <= menor) entao
              menor <- N
           senao
           FimSe
        FimSe
        S <- S + N
        cont <- cont + 1
    FimEnquanto
    Escreval("A soma de todos os valores é: ", S)
    Escreval("O maior número é: ", maior)
    Escreval("O menor número é: ", menor)
fimalgoritmo

_____________________________________________________________________________________________________________

algoritmo "conversor dólar"
var
    R, D: real
    C, Q: inteiro
inicio
    C <- 1
    Escreva("Quantas vezes você deseja converter? ")
    Leia(Q)
    Enquanto (C <= Q) faca
       Escreva("Qual o valor em R$? ")
       Leia(R)
       D <- R/5.16
       Escreval("Valor convertido em dólar: US$ ", D:5:2)
       C <- C + 1
    FimEnquanto
fimalgoritmo

_____________________________________________________________________________________________________________

Aula 11 (Curso em vídeo) - Estruturas de Repetição 3	
algoritmo "QuantosDe0a10"
var
   C, V, Tot010, SImp: inteiro
inicio
      Tot010 <- 0
      SImp <- 0
      Para C <- 1 ate 6 faca
           Escreva("Digite um valor: ")
           Leia(V)
           Se (V >=0) e (V <=10) entao
              Tot010 <- Tot010 + 1
              Se (V>10)e(V%2=1) entao
                 SImp <- SImp + V
              FimSe
           FimSe
      FimPara
      EscrevaL("Ao todo foram ", Tot010, " valores entre 0 e 10.")
      EscrevaL("A soma de impares foi ", SImp,".")
fimalgoritmo
_____________________________________________________________________________________________________________
algoritmo "QuantosDe0a10SomaDeTodosImp"
var
   C, V, Tot010, SImp: inteiro
inicio
      Tot010 <- 0
      SImp <- 0
      Para C <- 1 ate 6 faca
           Escreva("Digite um valor: ")
           Leia(V)
           Se (V >=0) e (V <=10) entao
              Tot010 <- Tot010 + 1
           FimSe
           Se (V%2=1) entao
                 SImp <- SImp + V
           FimSe
      FimPara
      EscrevaL("Ao todo foram ", Tot010, " valores entre 0 e 10.")
      EscrevaL("A soma de impares foi ", SImp,".")
fimalgoritmo
_____________________________________________________________________________________________________________
algoritmo "Combinacoes"
var
  C1, C2: inteiro
inicio
      Para C1 := 1 ate 3 faca
           Para C2 := 1 ate 3 faca
                EscrevaL (C1, C2)
           FimPara
      FimPara
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "SeqFibonacci"
var
  C, T1, T2, T3: inteiro
inicio
  T1 <- 0
  Escreva(T1)
  T2 <- 1
  Escreva(T2)
  Para C := 3 ate 15 faca
       T3 := T1 + T2
       Escreva(T3)
       T1 := T2
       T2 := T3
  FimPara
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "AnalisadorDeValores"
var
  C, V, S, Di5, Nul, SomP: inteiro
  M: real
inicio
      S <- 0
      Di5 <- 0
      Nul <- 0
      SomP <- 0
      Para C := 1 ate 5 faca
           EscrevaL("Digite o ", C, "o. valor: ")
           Leia(V)
           S <- S + V
           Se (V%5=0) entao
              Di5 <- Di5 + 1
           FimSe
           Se (V=0) entao
              Nul <- Nul +1
           FimSe
           Se (V%2=0) entao
              SomP <- SomP + V
           FimSe
      FimPara
      M <- S/(C-1)
      EscrevaL("A soma entre os valores é: ", S)
      EscrevaL("A média entre os valores é: ", M:5:1)
      EscrevaL("Valores divisiveis por 5: ", Di5)
      EscrevaL("Valores nulos: ", Nul)
      EscrevaL("A soma dos valores pares é: ", SomP)
fimalgoritmo
_____________________________________________________________________________________________________________
- Aula 12 (Curso em vídeo) - Procedimentos
algoritmo "MaisPesado"
var
   I: inteiro
   O, Pes: caractere
   P, Mai: real
Procedimento Topo()
inicio
      LimpaTela
      EscrevaL("---------------------------------------")
      EscrevaL("---- D E T E C T O R  D E  P E S O ----")
      EscrevaL("   O objeto mais pesado pesa, ", Mai, "Kg")
      EscrevaL("---------------------------------------")
FimProcedimento
inicio
      Topo()
      Para I := 1 ate 5 faca
           Escreva("Digite o objeto: ")
           Leia(O)
           Escreva("Digite o peso do ", O, ": ")
           Leia(P)
           Se (P>Mai) entao
              Mai <- P
              Pes <- O
           FimSe
           Topo()
      FimPara
      Topo()
      EscrevaL("O objeto mais pesado foi: ", Pes, ", com ", Mai, "Kg.")
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "ProcPorValorSoma"
var
 X, Y: Inteiro
Procedimento Soma(A, B: Inteiro)
inicio
      EscrevaL("Recebi o valor ", A)
      EscrevaL("Recebi o valor ", B)
      EscrevaL("A soma entre os valores é: ", A+B)
FimProcedimento
inicio
      EscrevaL("Digite o primeiro valor: ")
      Leia(X)
      EscrevaL("Digite o segundo valor: ")
      Leia(Y)
      LimpaTela
      Soma(X,Y)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "ProcParOuImpar"
var
  N: inteiro
Procedimento ParOuImpar(V: inteiro)
Inicio
      Se (V%2=0) entao
         EscrevaL("O número ", V, " é par!")
      senao
         EscrevaL("O número ", V, " é impar!")
      FimSe
FimProcedimento
inicio
      Escreva("Digite um número: ")
      Leia(N)
      ParOuImpar(N)
fimalgoritmo

 - **ESCOPO: É O LOCAL ONDE DETERMINADA VARIÁVEL IRÁ FUNCIONAR**
 _____________________________________________________________________________________________________________
 
 algoritmo "SomaParametrosRef"
var
  X, Y: Inteiro
Procedimento Soma(var A, B: inteiro)
Inicio
      A <- A + 1
      B <- B + 2
      EscrevaL("Valor de A = ", A)
      EscrevaL("Valor de B = ", B)
      EscrevaL("A soma entre A e B = ", A+B)
FimProcedimento
inicio
      X <- 4
      Y <- 7
      Soma(X, Y)
      EscrevaL("Valor de X = ", X)
      EscrevaL("Valor de Y = ", Y)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "Fibonacci"
var
  C, T1, T2, T3: inteiro
Procedimento ProximoFibonacci(var A, B: inteiro)
var C: inteiro
inicio
    C := A + B
    Escreva(C)
    A := B
    B := C
FimProcedimento
inicio
      T1 <- 0
      Escreva(T1)
      T2 <- 1
      Escreva(T2)
      Para C := 3 ate 10 faca
           ProximoFibonacci(T1, T2)
      FimPara
fimalgoritmo
_____________________________________________________________________________________________________________
- Aula 13 (Curso em vídeo) - Funções

algoritmo "SomaFuncao"
var
   V1, V2, S: inteiro
funcao Soma(X, Y: inteiro):inteiro
inicio
   Retorne X + Y
fimfuncao
inicio
   Escreva("Digite o primeiro valor: ")
   Leia(V1)
   Escreva("Digiten o segundo valor: ")
   Leia(V2)
   S <- Soma(V1, V2)
   EscrevaL("A soma entre", V1, " e", V2, " é igual a:",S)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "FunParImpar"
var
N: inteiro
R: caractere
funcao ParOuImpar(V: inteiro) : caractere
inicio
    Se (V%2=0)entao
       Retorne "PAR"
    senao
       Retorne "IMPAR"
    fimse
fimfuncao
inicio
      Escreva("Digite um número: ")
      Leia(N)
      R <- ParOuImpar(N)
      Escreva("O número ", N, " é um valor ", R)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "FunFatorial"
var
N,F: inteiro
funcao Fatorial(V: inteiro) : inteiro
var C,R: inteiro
inicio
   R <- 1
   Para C := 1 ate V faca
       R<- R*C
   Fimpara
   Retorne R
fimfuncao
inicio
      Escreva("Digite um número: ")
      Leia(N)
      F <- Fatorial(N)
      EscrevaL("O valor de ",N,"! é igual a ", F)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "FunFibonacci"
var
  C, T1, T2, T3: inteiro
funcao ProximoFibonacci(var N1, N2: inteiro):inteiro
var
N3: inteiro
inicio
    N3 := N1 + N2
    N1 := N2
    N2 := N3
    Retorne N3
fimfuncao
inicio
      T1 <- 0
      Escreval(T1)
      T2 <- 1
      Escreval(T2)
      Para C := 3 ate 10 faca
           T3 <- ProximoFibonacci(T1, T2)
           EscrevaL(T3)
      FimPara
fimalgoritmo

- **FUNÇÕES PRÉ DEFINIDAS - VISUALG
SITE: "BancoDeDados"

Compr(Site)  12
Copia(Site, 6, 2)  De
Maiusc(Site)  BANCODEDADOS
Minusc(Site)  bancodedados
Pos("Dados", Site)  8
Asc("b")  98 - código da letra
Asc("B")  66
Asc("C")  67
Carac(67)  C

algoritmo "AnalisadoStrings"
var
N: caractere
C: inteiro
inicio
   Escreva("Digite seu nome: ")
   Leia(N)
   EscrevaL("Total de letras do seu nome: ", Compr(N))
   EscrevaL("Seu nome em maiusculo: ", Maiusc(N))
   EscrevaL("Seu nome em minusculo: ", Minusc(N))
   EscrevaL("A primeira letra do seu nome é: ", Copia(N, 1,1))
   EscrevaL("A ultima letra do seu nome é: ", Copia(Maiusc(N), Compr(N), 1))
   EscrevaL("Posição da letra A no seu nome: ", Pos("A", Maiusc(N)))
   EscrevaL("O código da letra B é: ", Asc("B"))
   EscrevaL("O numero 43 se refere ao caractere ", Carac(43))
   
   Para C <- Compr(N) ate 1 passo -1 faca
      Escreva(Copia(Maiusc(N), C,1))
   FimPara
fimalgoritmo
_____________________________________________________________________________________________________________
- Aula 14 (Curso em vídeo) - Vetores

algoritmo "TesteVetor"
var
  V: vetor[1..6] de inteiro
  C: inteiro
inicio
  Para C <- 1 ate 6 faca
       Escreva("Digite o ", C, "o. valor: ")
       Leia(v[c])
  FimPara
  Para C <- 1 ate 6 faca
       Escreva("{",v[c],"}")
  FimPara
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "ValoresPares"
var
  val: vetor[1..7] de inteiro
  ind, totpar: inteiro
inicio
   totpar <- 0
   Para ind <- 1 ate 7 faca
        Escreva("Digite o ", ind, "o. valor: ")
        Leia(val[ind])
        Se (val[ind]%2=0) entao
            totpar <- totpar + 1
        FimSe
   FimPara
   EscrevaL("O total de números pares digitados foram ", totpar)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "ValoresParesPosição"
var
  val: vetor[1..7] de inteiro
  ind, totpar: inteiro
inicio
   totpar <- 0
   Para ind <- 1 ate 7 faca
        Escreva("Digite o ", ind, "o. valor: ")
        Leia(val[ind])
   FimPara
   Para ind <- 1 ate 7 faca
        Se (val[ind]%2=0) entao
           totpar <- totpar + 1
           EscrevaL("Valor PAR na Posição ", ind)
        FimSe
   FimPara
   EscrevaL("** O total de números pares: ", totpar, " **")
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "TurmaA"
var
   nome: vetor[1..4] de caractere
   n1: vetor[1..4] de real
   n2: vetor[1..4] de real
   m: vetor[1..4] de real
   c, totA: inteiro
   sm, mt: real
inicio
   Para c <- 1 ate 4 faca
        EscrevaL("ALUNO ", c)
        Escreva("Nome: ")
        Leia(nome[c])
        Escreva("Primeira nota: ")
        Leia(n1[c])
        Escreva("Segunda nota: ")
        Leia(n2[c])
        m[c] <- (n1[c]+n2[c])/2
        sm <- sm + m[c]
   FimPara
   mt <- sm/4
   LimpaTela
   EscrevaL("Listagem de Alunos")
   EscrevaL("------------------")
   Para c <- 1 ate 4 faca
        EscrevaL(nome[c]:15, m[c]:4:1)
        Se (m[c] > mt) entao
           totA <- totA + 1
        FimSe
   FimPara
   EscrevaL("Ao todo, temos ", totA, " alunos que estão acima da média da turma, que é: ", mt:4:1)
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "SoComR"
var
  nome: caractere
  soR: vetor[1..10] de caractere
  c, tot: inteiro
inicio
  tot <- 0
  Para c <- 1 ate 10 faca
     Escreva("Digite o ", c, "o. nome: ")
     Leia(nome)
     Se (copia(maiusc(nome), 1, 1) = "R") entao
        tot <- tot + 1
        soR[tot] <- nome
     FimSe
  FimPara
  LimpaTela
  EscrevaL("LISTAGEM FINAL")
  Para c <- 1 ate tot faca
     EscrevaL(soR[c])
  FimPara
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "OrdenaVetor"
var
   vet: vetor[1..4] de inteiro
   i,j, aux: inteiro
inicio
      Para i <- 1 ate 10 faca
           Escreva("Digite um valor: ")
           Leia(vet[i])
      FimPara
      
      Para i <- 1 ate 9 faca
           Para j <- i+1 ate 10 faca
                Se (vet[i] > vet[j]) entao
                    aux <- vet[i]
                    vet[i] <- vet[j]
                    vet[j] <- aux
                FimSe
           FimPara
      FimPara
      
      Para i <- 1 ate 10 faca
           Escreva("{",(vet[i]),"}")
      FimPara
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "TimeXTime"
var
l, c: inteiro
time: vetor[1..3] de caractere
procedimento Topo()
inicio
   Escreval("---------------------")
   Escreval("CAMPEONATO DE FUTEBOL")
   Escreval("---------------------")
fimprocedimento
inicio
   Topo()
   Para c <- 1 ate 3 faca
       Escreva("Nome do ", c, "o. time: ")
       Leia(time[c])
   FimPara
   
   LimpaTela
   Topo()
   Para l <- 1 ate 3 faca
        Para c <- 1 ate 3 faca
            Se (l <> c) entao
                EscrevaL(time[l]:12, "[ ] x [ ] ", time[c]:12)
            FimSe
        FimPara
   FimPara
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "ContagemDeNotas"
var
  gab: vetor[1..5] de caractere
  prova: vetor[1..5] de caractere
  nome: vetor[1..3] de caractere
  nota: vetor[1..3] de real
  s, m: real
  a: inteiro
  
procedimento CadastroGabarito()
var
  c: inteiro
inicio
    EscrevaL("CADASTRO DO GABARITO")
    EscrevaL("--------------------")
    Para c <- 1 ate 5 faca
         Escreva("Questão ", c, ": ")
         Leia(gab[c])
    FimPara
fimprocedimento

funcao CadastraProva():real
var
  c: inteiro
  notaFinal: real
inicio
   notaFinal <- 0
   EscrevaL("RESPOSTAS DADAS")
   Para c <- 1 ate 5 faca
        Escreva("Questão ", c, ": ")
        leia(prova[c])
        Se (maiusc(prova[c]) = maiusc(gab[c])) entao
           notaFinal <- notaFinal + 2
        FimSe
   FimPara
   Retorne notaFinal
fimfuncao

inicio
   CadastroGabarito()
   
   Para a <- 1 ate 3 faca
        LimpaTela
        EscrevaL("---------------")
        EscrevaL("ALUNO ", a)
        EscrevaL("---------------")
        Escreva("Nome: ")
        Leia(nome[a])
        nota[a] <- CadastraProva()
        s <- s + nota[a]
   FimPara
   
   LimpaTela
   Escreval("NOTAS FINAIS")
   EscrevaL("---------------")
   Para a <- 1 ate 3 faca
        EscrevaL(nome[a]:10, nota[a]:4:1)
   FimPara
   m <- s/3
   EscrevaL("---------------------")
   Escreva("Média da turma: ", m:4:1)
fimalgoritmo
_____________________________________________________________________________________________________________
- Aula 15 (Curso em vídeo) - Matrizes

algoritmo "Matriz"
var
mat: vetor[1..3, 1..2] de inteiro
l,c: inteiro
inicio
    Para l <- 1 ate 3 faca
       Para c <- 1 ate 2 faca
          Escreva("Digite o valor da casa [",l,",",c,"]: ")
          Leia(mat[l,c])
       FimPara
    FimPara
    Escreval()
    Para l <- 1 ate 3 faca
       Para c <- 1 ate 2 faca
           Escreva(mat[l,c]:5)
       FimPara
       Escreval()
    FimPara
fimalgoritmo
_____________________________________________________________________________________________________________

algoritmo "MatrizPares"
var
mat: vetor[1..3, 1..3] de inteiro
l,c, numP: inteiro
inicio
    Para l <- 1 ate 3 faca
       Para c <- 1 ate 3 faca
          Escreva("Digite o valor da casa [",l,",",c,"]: ")
          Leia(mat[l,c])
       FimPara
    FimPara
    Escreval()
    Escreva("MATRIZ:")
    Escreval()
    Para l <- 1 ate 3 faca
       Para c <- 1 ate 3 faca
           Se(mat[l,c] % 2 = 0) entao
              Escreva("{", mat[l,c]:4,"}")
           senao
              Escreva(mat[l,c]:6)
           FimSe
       FimPara
       Escreval()
    FimPara
fimalgoritmo
